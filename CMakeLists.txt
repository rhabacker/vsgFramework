cmake_minimum_required(VERSION 3.7)

project(vsgFramework)

include(ExternalProject)

# Change the default build type to Release
if(NOT CMAKE_BUILD_TYPE)
    set(CMAKE_BUILD_TYPE Release CACHE STRING "Choose the type of build, options are: None Debug Release RelWithDebInfo MinSizeRel." FORCE)
endif(NOT CMAKE_BUILD_TYPE)

if(NOT CONFIGURE_COMMAND)
    set(CONFIGURE_COMMAND ${CMAKE_COMMAND})
endif()

set(DOWNLOADS_DIR ${PROJECT_BINARY_DIR}/downloads)
set(COMPONENTS_DIR ${PROJECT_BINARY_DIR})
set(COMPONENT_INSTALL_PREFIX ${PROJECT_BINARY_DIR} CACHE STRING "Install prefix for components ")
set(COMPONENT_COMMON_CMAKE_OPTIONS
    -DCMAKE_BUILD_TYPE=${CMAKE_BUILD_TYPE}
    -DCMAKE_INSTALL_PREFIX=${COMPONENT_INSTALL_PREFIX}
    -DBUILD_SHARED_LIBS=${BUILD_SHARED_LIBS}
    -DCMAKE_PREFIX_PATH=${COMPONENT_INSTALL_PREFIX}
    -DCONFIGURE_COMMAND=${CONFIGURE_COMMAND}
)

install(DIRECTORY ${INSTALL_PREFIX}/bin DESTINATION .)
install(DIRECTORY ${INSTALL_PREFIX}/lib DESTINATION .)
install(DIRECTORY ${INSTALL_PREFIX}/include DESTINATION .)

add_custom_target(clean-all)
add_custom_target(clobber
    COMMAND git -C ${CMAKE_SOURCE_DIR} clean -d -f -x
)

add_custom_target(clobber-all)

##############################################################################################################
#
# VulkkanSDK
#
if (VULKAN_SDK)
    set(ENV{VULKAN_SDK} ${VULKAN_SDK})
endif()

find_package(Vulkan)

if (NOT VULKAN_FOUND)
    set(VulkanSDKVersion "1.2.176.1" CACHE STRING "Vulkan_SDK version to download")

    if (NOT EXISTS ${DOWNLOADS_DIR}/${VulkanSDKVersion})
        file(MAKE_DIRECTORY downloads)

        if (ANDROID)
            # TODO : Need to implement Andoid VulkanSDK download and unpack
        elseif (WIN32)
            # TODO : Need to implement Windows VulkanSDK download and unpack
        elseif (APPLE)
            # TODO : Need to implement MacOS VulkanSDK download and unpack
            # Possible something like:
            # execute_process(COMMAND wget --no-cookies -O ../vulkansdk-macos-${{env.VulkanSDKVersion}}.dmg https://sdk.lunarg.com/sdk/download/${{env.VulkanSDKVersion}}/mac/vulkansdk-macos-{{env.VulkanSDKVersion}}.dmg)
            # then mount the dmg in some fashion
        else()
            # linux
            execute_process(COMMAND wget --no-cookies -O ${DOWNLOADS_DIR}/vulkansdk-linux-x86_64-${VulkanSDKVersion}.tar.gz https://sdk.lunarg.com/sdk/download/${VulkanSDKVersion}/linux/vulkansdk-linux-x86_64-${VulkanSDKVersion}.tar.gz)
            execute_process(COMMAND tar -zxf ${DOWNLOADS_DIR}/vulkansdk-linux-x86_64-${VulkanSDKVersion}.tar.gz -C ${DOWNLOADS_DIR})
        endif()
    endif()

    if (EXISTS ${DOWNLOADS_DIR}/${VulkanSDKVersion})
        set(VULKAN_SDK ${DOWNLOADS_DIR}/${VulkanSDKVersion}/x86_64)
        set(ENV{VULKAN_SDK} ${VULKAN_SDK})
        set(EXTRA_CMAKE_ARGS_VULKAN_SDK -DVULKAN_SDK=${VULKAN_SDK})

        find_package(Vulkan)
    endif()
endif()

# ExternalComponent macro to help set up external projects and their depdendecies
# Usage:
#   ExternalComponent(component_name git_repository_url git_tag [optional_component_dependencies...])
macro(ExternalComponent component_name git_repository git_tag)
    # migrate deprecated setting
    if(build_${component_name})
        set(BUILD_${component_name} ON)
        unset(build_${component_name})
    endif()
    if (BUILD_${component_name})
        set(${component_name} "${component_name}")
        set(binary_dir build/${component_name})

        ExternalProject_Add(${component_name}
            PREFIX ${COMPONENTS_DIR} 
            GIT_PROGRESS ON
            GIT_REPOSITORY ${git_repository} 
            GIT_TAG ${git_tag}
            CONFIGURE_COMMAND
                ${CONFIGURE_COMMAND}
                    -S ${COMPONENTS_DIR}/src/${component_name}
                    ${COMPONENT_COMMON_CMAKE_OPTIONS}
                    ${COMPONENTS_CMAKE_OPTIONS}
                    ${${component_name}_CMAKE_OPTIONS}
            BINARY_DIR ${binary_dir}
            INSTALL_DIR ${COMPONENTS_DIR}
        )
        
        ExternalProject_Add_StepDependencies(${component_name} build "${ARGN}")

        add_custom_target(clean-${component_name}
            COMMAND ${CMAKE_COMMAND} --build ${CMAKE_BINARY_DIR}/${binary_dir}
                                     --target clean
        )
        add_dependencies(clean-all clean-${component_name})

        add_custom_target(clobber-${component_name}
            COMMAND git -C ${CMAKE_BINARY_DIR}/src/${component_name} clean -d -f -x
        )
        add_dependencies(clobber-all clobber-${component_name})
    endif()
endmacro()

OPTION(BUILD_SHARED_LIBS "Build shared libraries" OFF)

# glslang
OPTION(BUILD_glslang "Build glslang" ON)
ExternalComponent(glslang "https://github.com/KhronosGroup/glslang.git" "master")

# VulkaSceneGraph
OPTION(BUILD_VulkanSceneGraph "Build VulkanSceneGraph" ON)
ExternalComponent(VulkanSceneGraph "https://github.com/vsg-dev/VulkanSceneGraph.git" "master" ${glslang})

# vsgGIS
OPTION(BUILD_vsgGIS "Build vsgGIS" ON)
ExternalComponent(vsgGIS "https://github.com/vsg-dev/vsgGIS.git" "master" ${VulkanSceneGraph})

# assimp
OPTION(BUILD_assimp "Build assimp" ON)
ExternalComponent(assimp "https://github.com/assimp/assimp.git" "master")

# vsgXchange
OPTION(BUILD_vsgXchange "Build vsgXchange" ON)
ExternalComponent(vsgXchange "https://github.com/vsg-dev/vsgXchange.git" "master" ${VulkanSceneGraph} ${vsgGIS} ${assimp})

# vsgImGui
OPTION(BUILD_vsgImGui "Build vsgImGui" ON)
ExternalComponent(vsgImGui "https://github.com/vsg-dev/vsgImGui.git" "master" ${VulkanSceneGraph} ${vsgXchange})

# vsgExamples
OPTION(BUILD_vsgExamples "Build vsgExamples" ON)
ExternalComponent(vsgExamples "https://github.com/vsg-dev/vsgExamples.git" "master" ${VulkanSceneGraph} ${vsgGIS} ${assimp} ${vsgXchange} ${vsgImGui})

# vsgQt
OPTION(BUILD_vsgQt "Build vsgQt" ON)
ExternalComponent(vsgQt "https://github.com/vsg-dev/vsgQt.git" "master" ${VulkanSceneGraph} ${vsgXchange})

# vsgUnity
OPTION(BUILD_vsgUnity "Build vsgUnity" OFF)
ExternalComponent(vsgUnity "https://github.com/vsg-dev/vsgUnity.git" "master" ${VulkanSceneGraph})

# MyFirstVsgApplication
OPTION(BUILD_MyFirstVsgApplication "Build MyFirstVsgApplication" OFF)
ExternalComponent(MyFirstVsgApplication "https://github.com/vsg-dev/MyFirstVsgApplication.git" "master" ${VulkanSceneGraph})
